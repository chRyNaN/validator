import com.jfrog.bintray.gradle.tasks.BintrayUploadTask
import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact

ext {
    bintrayRepo = "chrynan"
    libraryGroup = "com.chrynan.validator"
    libraryOwner = "chrynan"
    libraryRepo = "validator"
    libraryVersion = "0.1.0"
    libraryVersionCode = 1
    libraryVersionDescription = "First Major Release"
    libraryGithubUrl = "https://github.com/chRyNaN/validator.git"
    libraryLicense = "Apache-2.0"
}

group libraryGroup
version libraryVersion

buildscript {
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.0"
        classpath "digital.wup:android-maven-publish:3.6.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }
}

allprojects {
    repositories {
        jcenter()
    }

    // Allows use of Experimental Kotlin features, such as, Kotlin Contracts
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental", "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "org.jetbrains.dokka"

    dokka {
        outputFormat = "gfm"
        outputDirectory = "../docs"

        configuration {
            includeNonPublic = false
            skipEmptyPackages = true
            skipDeprecated = true
            reportUndocumented = true
            jdkVersion = 8
            targets = ["JVM", "COMMON"]
        }
    }

    bintray {
        user = project.findProperty("BINTRAY_USER") ?: System.getenv("BINTRAY_USER")
        key = project.findProperty("BINTRAY_KEY") ?: System.getenv("BINTRAY_KEY")
        pkg {
            repo = rootProject.bintrayRepo
            name = rootProject.libraryRepo
            licenses = [rootProject.libraryLicense]
            vcsUrl = rootProject.libraryGithubUrl
            version {
                name = rootProject.libraryVersion
                desc = rootProject.libraryVersionDescription
                released = new Date()
                vcsTag = rootProject.libraryVersion
            }
        }
    }

    bintrayUpload.doFirst {
        publications = publishing.publications.collect {
            it.name
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal

    tasks.withType(BintrayUploadTask) {
        doFirst {
            publishing.publications.withType(MavenPublication).forEach({ publication ->
                File moduleFile = new File(project.buildDir, "publications/${publication.name}/module.json")
                if (moduleFile.exists()) {
                    publication.artifact(new FileBasedMavenArtifact(moduleFile) {
                        protected String getDefaultExtension() {
                            return "module"
                        }
                    })
                }
            })
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}