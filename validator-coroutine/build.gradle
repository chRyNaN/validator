import com.chrynan.validator.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    android {
        publishAllLibraryVariants()
        publishLibraryVariantsGroupedByFlavor = true
    }
    targets {
        android()
        jvm()
        js(BOTH) {
            browser {
                testTask {
                    useKarma {
                        useFirefox()
                    }
                }
            }
            nodejs()
        }
        ios()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"

                implementation project(":validator-core")
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
        androidTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js"
            }
        }
    }
}

android {
    compileSdkVersion LibraryConstants.Android.compileSdkVersion

    defaultConfig {
        minSdkVersion LibraryConstants.Android.minSdkVersion
        targetSdkVersion LibraryConstants.Android.targetSdkVersion
        versionCode LibraryConstants.versionCode
        versionName LibraryConstants.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    repositories {
        maven {
            url "https://repo.repsy.io/mvn/chrynan/public"

            credentials {
                username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
