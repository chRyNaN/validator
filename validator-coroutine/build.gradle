import com.chrynan.validator.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"
apply plugin: "com.chrynan.target.plugin"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    targets {
        jvm()
        js()
        ios()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"

                implementation project(":validator-core")
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js"
            }
        }
    }
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    repositories {
        maven {
            url "https://repo.repsy.io/mvn/chrynan/public"

            credentials {
                username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
            }
        }
    }
}

// Target Information
kotlinTargetDataGenerator {
    generateJson = true
    generateSvg = true
    outputPath = "${projectDir.path}/targets"
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
